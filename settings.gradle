pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Ornithe'
			url = 'https://maven.ornithemc.net/releases'
		}
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release/'
		}
		gradlePluginPortal()
	}
}

boolean checkProp(String property) {
	if (!hasProperty(property))
		return false

	String value = getProperty(property).toLowerCase()
	return !(value.isEmpty() || value == "false" || value == "f")
}

void optional(String project) {
	if (checkProp(project) || checkProp("all"))
		include project
}

optional '1.8.9'
include 'common'
optional '1.19.3'
optional '1.19.2'
optional '1.16_combat-6'
optional '1.16.5'
optional '1.19.4'
include '1.20'

